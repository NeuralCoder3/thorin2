// RUN: rm -f %t.ll ; \
// RUN: %thorin -d autodiff .d direct -d clos -d math %s --output-ll %t.ll --output-thorin -
// RUN: clang %S/lib.c %t.ll -o %t -Wno-override-module
// RUN: %t 2 3 | FileCheck %s

.import core;
.import autodiff;
.import mem;

.let I32 = .Idx 4294967296;
.con printIntegerNL [%mem.M, I32, .Cn [%mem.M]];

.con f [[mem : %mem.M, a: %mem.Ptr («100:.Nat; I32», 0)], ret: .Cn [mem : %mem.M, I32]] = {
    .let lea = %mem.lea (100:.Nat, <100:.Nat; I32>, 0) (a, 1:(.Idx 100));
    .let (load_mem, load_val) = %mem.load (I32, 0) (mem, lea);
    .let scaled = %core.wrap.mul 4294967296 0 (2:I32, load_val);
    ret (load_mem, scaled)
    // 2*a[1]
};

.con .extern main [mem : %mem.M, argc : I32, argv : %mem.Ptr (%mem.Ptr (.Idx 256, 0:.Nat), 0:.Nat), return : .Cn [%mem.M, I32]] = {

    .con ret_cont [mem : %mem.M, a: I32] = {
        printIntegerNL(mem, a, .cn [mem:%mem.M] = {
                return (mem, 0:I32)
        })
    };

    .let (alloc_mem, alloc_val) = %mem.alloc («100:.Nat; I32», 0) mem;

    .let lea = %mem.lea (100:.Nat, <100:.Nat; I32>, 0) (alloc_val, 1:(.Idx 100));
    .let store = %mem.store (I32, 0) (alloc_mem, lea, argc);


    f ((store, alloc_val),ret_cont)
};

// CHECK: 6
